use crate::lexer::*;
use crate::ast::*;

grammar;

pub SourceUnit:SourceUnit = {
    SourceUnitPart+ => SourceUnit(<>)
}

SourceUnitPart:SourceUnitPart = {
    <e:Expression> => SourceUnitPart::Expression(e),
    Statement => SourceUnitPart::Statement(<>),
}

Statement:Statement = {
    Declaration <id:Identifier> ";" => Statement::Declaration(id),
    Assignment => Statement::Allocation(<>),
    Write <e:Expression> ";" => Statement::WriteExpr(e),
    Write <s:StringLiteral> ";" => Statement::WriteString(s),
    If <condition:Conditional> "{" <s:SourceUnit> "}" => Statement::Conditional(condition,s,None),
    If <condition:Conditional> "{" <s:SourceUnit> "}" Else "{" <f:SourceUnit> "}" => Statement::Conditional(condition,s,Some(f)),
    Loop <condition:Conditional> "{" <s:SourceUnit> "}" => Statement::Loop(condition,s)
}

Assignment:Expression = {
    <l:Identifier> "=" <r:StringLiteral> ";" => Expression::StringAlloc(l,r),
    <l:Identifier> "=" <r:Expression> ";" => Expression::Assignment(l,Box::new(r)),
}

Conditional:Expression = {
    <l:Expression>  <r:Expression> "==" => Expression::Equals(Box::new(l),Box::new(r)),
    <l:Expression>  <r:Expression> "!=" => Expression::NotEquals(Box::new(l),Box::new(r)),
    <l:Expression>  <r:Expression> ">" => Expression::GreaterThan(Box::new(l),Box::new(r)),
    <l:Expression>  <r:Expression> "<" => Expression::LessThan(Box::new(l),Box::new(r)),
}

Expression:Expression = {
    ArithExpression 
}

ArithExpression: Expression = {
    <l:Expression> "+" <r:Factor> => Expression::Add(Box::new(l),Box::new(r)),
    <l:Expression> "-" <r:Factor> => Expression::Subtract(Box::new(l),Box::new(r)),
    Factor,
}

Factor: Expression = {
    <l:Factor> "*" <r:Term> => Expression::Multiply(Box::new(l),Box::new(r)),
    <l:Factor> "/" <r:Term> => Expression::Divide(Box::new(l),Box::new(r)),
    Term,
};

Term: Expression = {
    <v:Integer> => Expression::Integer(v),
    <id:Identifier> => Expression::Symbol(id),
    "(" <Expression> ")"
};

extern {
    type Location = usize;
    type Error = LexicalError;

    enum TokenType {
        Identifier => TokenType::Symbol(String),
        ";" => TokenType::SemiColon,
        Declaration => TokenType::Declaration,
        "=" => TokenType::Assignment,
        Integer => TokenType::Number(i64),
        StringLiteral => TokenType::Literal(String),
        "+" => TokenType::Plus,
        "-" => TokenType::Minus,
        "*" => TokenType::Product,
        "/" => TokenType::Divide,
        "(" => TokenType::OpenParantheses,
        ")" => TokenType::CloseParantheses,
        Write => TokenType::Write,
        If => TokenType::If,
        Else => TokenType::Else,
        ">" => TokenType::GreaterThan,
        "<" => TokenType::LessThan,
        "==" => TokenType::EqualTo,
        "!=" => TokenType::NotEqual,
        "{" => TokenType::LeftBrace,
        "}" => TokenType::RightBrace,
        Loop => TokenType::Loop,
    }
}